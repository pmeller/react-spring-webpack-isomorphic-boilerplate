buildscript {
  ext {
    springBootVersion = '1.5.6.RELEASE'
  }
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("com.moowork.gradle:gradle-node-plugin:1.1.1")
  }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

version = '0.0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

configurations {
  dev
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-web')

  dev('org.springframework.boot:spring-boot-devtools')

  testCompile('org.springframework.boot:spring-boot-starter-test')
}

task yarnRunBuild(type: YarnTask) {
  args = ['run', 'build']
}

bootRun {
  classpath = sourceSets.main.runtimeClasspath + configurations.dev
  jvmArgs = ["-Dspring.output.ansi.enabled=ALWAYS"] // colored output for better readability
}

processResources.finalizedBy('yarn', 'yarnRunBuild')
